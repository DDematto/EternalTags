import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group "xyz.oribuin"
version "1.1.7"

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-parameters']
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://libraries.minecraft.net" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://jitpack.io" }

    maven { url "https://repo.mattstudios.me/artifactory/public/" }

}

dependencies {
    // General Dependencies
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly "com.github.oraxen:oraxen:afc4903680"

    // Frameworks & APIs
    implementation "dev.rosewood:rosegarden:1.1.0.48-SNAPSHOT"
    implementation('dev.triumphteam:triumph-gui:3.1.4') {  // https://mf.mattstudios.me/triumph-gui/introduction
        exclude group: "com.google.code.gson", module: "gson" // Remove GSON, Already included in spigot api
    }
}


shadowJar {
    archiveClassifier.set(null)
    minimize {
        exclude(dependency("dev.rosewood:rosegarden:.*"))
    }

    relocate("dev.rosewood.rosegarden", "${project.group}.eternaltags.libs.rosegarden")
    relocate("dev.triumphteam.gui", "${project.group}.eternaltags.libs.gui")
    relocate("net.kyori", "${project.group}.eternaltags.libs.kyori")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}


tasks.getByName("build").dependsOn("shadowJar")